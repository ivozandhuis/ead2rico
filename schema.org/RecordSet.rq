PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rico: <https://www.ica.org/standards/RiC/ontology#> 
PREFIX sdo: <https://schema.org/> 

CONSTRUCT {
    ?resource a sdo:ArchiveComponent ;
        sdo:additionalType ?type ;
        sdo:isPartOf ?parent ;
        sdo:url ?webpage ;
        sdo:image ?iiif_view ;
        sdo:identifier ?identifier ;
        sdo:name ?title ;
        sdo:size ?extent ;
        sdo:dateCreated ?date1 ;
        sdo:dateCreated ?date2 ;
        sdo:dateCreated ?date3 ;
        sdo:inLanguage ?lang ;
        sdo:creator ?creator ;
        sdo:holdingArchive ?repository ;
        sdo:about ?subject ;
        sdo:description ?description ;
        sdo:description ?scopecontent ;
        sdo:description ?history ;
        sdo:description ?access ;
        sdo:description ?use ;
        sdo:description ?structure .
}
WHERE
{

    # BIND(<https://hdl.handle.net/10622/ARCH00860#3-6> AS ?resource)

    ?resource a rico:RecordSet ;
        rico:hasOrHadType ?type .

    OPTIONAL {
        ?resource rico:isOrWasIncludedIn ?parent .
    }

    OPTIONAL {
        ?resource rico:isOrWasDescribedBy ?webpage .
        ?webpage a rico:Record ;
            rico:hasOrHadType <http://vocab.getty.edu/aat/300264578> .
    }

    OPTIONAL {
        ?resource rico:hasOrHadInstantiation ?scans .
        ?scans a rico:Instantiation ;
            rico:isOrWasDescribedBy ?IIIFmanifest .
        ?IIIFmanifest a rico:Record ;
            rico:hasOrHadType <https://www.ica.org/standards/RiC/vocabularies/documentaryFormTypes#iiif-manifest> .

        BIND("https://access.iisg.amsterdam/universalviewer/#?" AS ?viewer)
        BIND(CONCAT(?viewer, "manifest=", str(?IIIFmanifest)) AS ?iiif_view)
    }

    OPTIONAL {
        ?resource rico:hasOrHadIdentifier/rico:textualValue ?identifier .
    }

    OPTIONAL {
        ?resource rico:hasOrHadTitle/rico:textualValue ?title .
    }

    OPTIONAL {
        ?resource rico:hasExtent/rico:textualValue ?extent .
    }

    OPTIONAL {
        ?resource rico:hasAccumulationDate/rico:textualValue ?date1 .
    }

    OPTIONAL {
        ?resource rico:hasOrHadMostMembersWithAccumulationDate/rico:textualValue ?date2 .
    }

    OPTIONAL {
        ?resource rico:hasOrHadAllMembersWithAccumulationDate/rico:textualValue ?date3 .
    }

    OPTIONAL {
        ?resource rico:hasOrHadSomeMembersWithLanguage ?lang .
    }

    OPTIONAL {
        ?resource rico:hasOrganicProvenance/rico:hasOrHadName/rico:textualValue ?creator .
    }

    # OPTIONAL {
    #     ?resource rico:hasOrHadLocation/rico:hasOrHadName/rico:textualValue ?location .
    # }

    OPTIONAL {
        ?resource rico:hasOrHadHolder/rico:hasOrHadName/rico:textualValue ?repository .
    }

    OPTIONAL {
        ?resource rico:hasOrHadSubject/rico:hasOrHadName/rico:textualValue ?subject .
    }

    OPTIONAL {
        ?resource rico:generalDescription ?description .
    }

    OPTIONAL {
        ?resource rico:scopeAndContent ?scopecontent .
    }

    OPTIONAL {
        ?resource rico:history ?history .
    }

    OPTIONAL {
        ?resource rico:conditionsOfAccess ?access .
    }

    OPTIONAL {
        ?resource rico:conditionsOfUse ?use .
    }

    OPTIONAL {
        ?resource rico:recordResourceStructure ?structure .
    }
}